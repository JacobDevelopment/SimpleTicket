plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.0.1'
    id 'nu.studer.jooq' version '8.2'
}

repositories {
    mavenCentral()
}

javafx {
    version = "16"
    modules = ['javafx.controls', 'javafx.fxml']
}

application {
    mainModule = "io.jacobking.simpleticket"
}


sourceSets {
    main {
        java {
            srcDirs += 'build/generated-src/jooq'
        }
    }
}

dependencies {
    implementation group: 'commons-io', name: 'commons-io', version: '2.14.0'
    implementation group: 'org.jooq', name: 'jooq', version: '3.18.6'
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.42.0.0'
    implementation group: 'org.controlsfx', name: 'controlsfx', version: '11.1.2'
    jooqGenerator 'org.xerial:sqlite-jdbc:3.42.0.0'
}

mainClassName = "io.jacobking.simpleticket.App"

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'SimpleTicket-beta'
        version = '1.1.1'
        description = 'A lightweight application to streamline the process of logging and managing IT support-related tickets locally on your machine.'
    }
    jpackage {
        installerOptions = [
                '--description', 'A lightweight application to streamline the process of logging and managing IT support-related tickets locally on your machine.',
                '--copyright', 'Copyright 2023-2024 JacobDevelopment'
        ]
        installerType = project.findProperty('installerType')
        imageOptions += ['--icon', 'src/main/resources/io/jacobking/simpleticket/icons/icon.ico']
        installerOptions += [
                '--win-per-user-install', '--win-dir-chooser',
                '--win-menu', '--win-shortcut',
                '--vendor', 'Jacob King',
                '--app-version', '1.1.1'
        ]
    }
}


import nu.studer.gradle.jooq.JooqEdition
import org.jooq.meta.jaxb.Logging

jooq {
    version = '3.18.6'  // default (can be omitted)
    edition = JooqEdition.OSS  // default (can be omitted)

    configurations {
        main {  // name of the jOOQ configuration
            generateSchemaSourceOnCompilation = true  // default (can be omitted)

            generationTool {
                logging = Logging.WARN
                jdbc {
                    driver = 'org.sqlite.JDBC'
                    url = 'jdbc:sqlite:testing.sqlite'
                    properties {
                    }
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.sqlite.SQLiteDatabase'
                        forcedTypes {
                        }
                    }
                    generate {
                        deprecated = false
                        records = true
                        pojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'io.jacobking.simpleticket'
                        directory = '/build/generated-src/jooq'
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}